buildscript {
    ext {
        protobufVersion = '3.22.0'
        protobufPluginVersion = '0.9.2'
        grpcVersion = '1.53.0'
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.7.8"
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.8'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'java-library'
    id 'com.google.protobuf' version "${protobufPluginVersion}"
    id 'org.liquibase.gradle' version '2.2.0'
}

group = 'dev.archie'
version = '0.0.2-SNAPSHOT'
sourceCompatibility = '17'

springBoot {
    buildInfo()
}

jar {
    enabled = false
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    clean {
        delete generatedFilesBaseDir
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('testcontainersVersion', "1.17.6")
    set('springCloudVersion', "2021.0.6")
}

dependencies {
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    testImplementation "org.testcontainers:mongodb:1.18.0"
    liquibaseRuntime 'org.mongodb:bson:4.9.0'
    liquibaseRuntime 'org.mongodb:mongodb-driver-core:4.9.0'
    liquibaseRuntime 'org.mongodb:mongodb-driver-sync:4.9.0'
    liquibaseRuntime 'org.liquibase.ext:liquibase-mongodb:4.20.0'
    liquibaseRuntime 'org.liquibase:liquibase-core:4.20.0'
    liquibaseRuntime 'info.picocli:picocli:4.6.1'
    liquibaseRuntime 'org.yaml:snakeyaml:2.0'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'net.devh:grpc-spring-boot-starter:2.14.0.RELEASE'
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5' // Java 9+ compatibility - Do NOT update to 2.0.0
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

liquibase {
    activities {
        main {
            changelogFile "src/main/resources/db/changelog/db.changelog-master.yml"
            url "mongodb://localhost:27017/handyman"
        }
    }
}